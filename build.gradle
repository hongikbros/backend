plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
}

group = 'com.hongikbros'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'dev' : profile

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "src/main/resources-$profile"
        }
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    print(project.property('profile'))
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    if ("${profile}" == 'dev') {
        implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.6.3'
    }

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    //rest-assured
    testImplementation 'io.rest-assured:rest-assured:4.3.3'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.3.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    testImplementation 'org.mockito:mockito-inline:2.13.0'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

processResources {
    copy {
        from "backend-secret"
        include "application.yml"
        into "src/main/resources"
    }
    copy {
        from "backend-secret"
        include "resources-${profile}/**"
        into "src/main"
    }
    copy {
        from "backend-secret"
        include "application-test.yml"
        into 'src/test/resources'
        rename('application-test.yml', 'application.yml')
    }
    copy {
        from "backend-secret"
        include "resources-test/**"
        into 'src/test'
    }
}